plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
    id 'com.adarshr.test-logger' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.43.0'
}

def final appName = 'Censo Custody'
def final coreAppId = "com.censocustody.android"

String versionNameMajor = 1
String versionNameMinor = 1
String versionNamePatch = 0

android {
    Boolean signBuild = false
    Properties configProperties = new Properties()

    if (file('../config.properties').exists()) {
        configProperties.load(new FileInputStream(file('../config.properties')))

        signBuild = configProperties['SIGN_BUILD'].toBoolean()
    }

    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    if (signBuild) {
        signingConfigs {
            config_release {

                keyAlias configProperties['RELEASE_KEY_ALIAS']
                keyPassword configProperties["RELEASE_STORE_PASSWORD"]
                storeFile file("keystore.jks")
                storePassword configProperties["RELEASE_STORE_PASSWORD"]
            }
        }
    }

    defaultConfig {
        applicationId coreAppId
        minSdk 31
        targetSdk 33
        versionCode 44

        buildConfigField "int", "versionNameMajor", versionNameMajor
        buildConfigField "int", "versionNameMinor", versionNameMinor
        buildConfigField "int", "versionNamePatch", versionNamePatch
        buildConfigField "String", "CORE_APP_ID", "\"$coreAppId\""

        versionName versionNameMajor + "." + versionNameMinor + "." + versionNamePatch
        if (signBuild) {
            signingConfig signingConfigs.config_release
        } else {
            signingConfig signingConfigs.debug
        }

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        manifestPlaceholders = [
                "appAuthRedirectScheme": "com.censocustody.android"
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            resValue "string", "app_name", appName
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://brooklyn.censocustody.com/\""
            buildConfigField "String", "MIN_VERSION_ENDPOINT", "\"censo-security-app-version.json\""
            buildConfigField "String", "ANCHOR_URL", "\"https://wallet.censocustody.com/\""
            resValue "string", "RAYGUN_APP_ID", "\"vuxX53AURVfZS87D1WPqeg\""
            debuggable false
        }
        preProd {
            resValue "string", "app_name", "PreProd Censo"
            applicationIdSuffix ".preprod"
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://brooklyn-preprod.censocustody.com/\""
            buildConfigField "String", "MIN_VERSION_ENDPOINT", "\"censo-security-app-version.json\""
            buildConfigField "String", "ANCHOR_URL", "\"https://preprod.censocustody.com/\""
            resValue "string", "RAYGUN_APP_ID", "\"CtOnGQjIo1U8dELkoUf0iw\""
            debuggable false
        }
        preRelease {
            resValue "string", "app_name", "PreRelease Censo"
            applicationIdSuffix ".prerelease"
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://brooklyn-prerelease.censocustody.com/\""
            buildConfigField "String", "MIN_VERSION_ENDPOINT", "\"censo-security-app-version-dev.json\""
            buildConfigField "String", "ANCHOR_URL", "\"https://prerelease.censocustody.com/\""
            resValue "string", "RAYGUN_APP_ID", "\"L9T2bPaEjr3Lede3SNpFJw\""
            debuggable false
        }
        debug {
            initWith preRelease
            resValue "string", "app_name", "Debug Censo"
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0-alpha01'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/okta/*'
            excludes += 'META-INF/DEPENDENCIES'
        }
    }
    lint {
        checkAllWarnings true
        lintConfig file('lint.xml')
        warningsAsErrors true
        disable 'GradleDependency'
    }
    namespace 'com.censocustody.android'
}

dependencies {

    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.android.material:material:1.7.0"
    implementation "androidx.compose.ui:ui:1.3.1"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.3.1"
    implementation "androidx.activity:activity-compose:1.6.1"
    debugImplementation "androidx.compose.ui:ui-tooling:1.3.1"

    // Compose Specific dependencies
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.exifinterface:exifinterface:1.3.5"

    // CameraX
    def camerax_version = "1.2.0"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    implementation "com.google.accompanist:accompanist-permissions:0.18.0"
    implementation "io.coil-kt:coil-compose:1.3.2"

    //Compose Accompanist
    implementation "com.google.accompanist:accompanist-flowlayout:0.17.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.23.1"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    // Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    //Firebase BOM
    implementation platform('com.google.firebase:firebase-bom:30.1.0') {
        exclude module: "play-services-ads-identifier"
        exclude module: "play-services-measurement"
        exclude module: "play-services-measurement-sdk"
        exclude module: "play-services-measurement-api"
    }
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4"

    //Push Notifications
    implementation 'com.google.firebase:firebase-messaging-ktx'

    //Encrypted Preferences
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha04'

    // Crypto utils
    implementation("org.web3j:core:5.0.0")  {
        exclude group: "org.bouncycastle"
    }

    implementation 'com.auth0.android:jwtdecode:2.0.1'

    //Biometrics
    implementation("androidx.biometric:biometric:1.1.0")

    // BIP39
    implementation "cash.z.ecc.android:kotlin-bip39:1.0.2"

    //Raygun crash reporting
    implementation 'com.raygun:raygun4android:4.0.1'

    // bitcoin
    implementation('org.bitcoinj:bitcoinj-core:0.16.1') {
        exclude group: "org.bouncycastle"
    }

    implementation 'org.bouncycastle:bcprov-jdk15to18:1.70'

    //Unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.mockito:mockito-inline:4.8.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"

    //UI testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.1"
}