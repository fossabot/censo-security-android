plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
    id 'com.adarshr.test-logger' version '3.1.0'
}

def final appName = 'Strike Mobile'
def final coreAppId = "com.strikeprotocols.mobile"

String versionNameMajor = 2
String versionNameMinor = 0
String versionNamePatch = 0

android {
    Boolean signBuild = false
    Properties configProperties = new Properties()

    if (file('../config.properties').exists()) {
        configProperties.load(new FileInputStream(file('../config.properties')))

        signBuild = configProperties['SIGN_BUILD'].toBoolean()
    }

    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    if (signBuild) {
        signingConfigs {
            config_release {

                keyAlias configProperties['RELEASE_KEY_ALIAS']
                keyPassword configProperties["RELEASE_STORE_PASSWORD"]
                storeFile file("keystore.jks")
                storePassword configProperties["RELEASE_STORE_PASSWORD"]
            }
        }
    }

    defaultConfig {
        applicationId coreAppId
        minSdk 26
        targetSdk 32
        versionCode 2

        buildConfigField "int", "versionNameMajor", versionNameMajor
        buildConfigField "int", "versionNameMinor", versionNameMinor
        buildConfigField "int", "versionNamePatch", versionNamePatch
        buildConfigField "String", "CORE_APP_ID", "\"$coreAppId\""

        versionName versionNameMajor + "." + versionNameMinor + "." + versionNamePatch
        if (signBuild) {
            signingConfig signingConfigs.config_release
        } else {
            signingConfig signingConfigs.debug
        }

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        buildConfigField "String", "OKTA_BASE_URL", "\"https://login.strikeprotocols.com\""
        buildConfigField "String", "OKTA_DOMAIN", "\"https://strike-prime.okta.com\""

        manifestPlaceholders = [
                "appAuthRedirectScheme": "com.strikeprotocols.mobile"
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            resValue "string", "app_name", appName
            minifyEnabled true
            buildConfigField "String", "BASE_URL", "\"https://brooklyn.strikeprotocols.com/\""
            buildConfigField "String", "ANCHOR_URL", "\"https://wallet.strikeprotocols.com/\""
            buildConfigField "String", "SOLANA_URL", "\"https://api.mainnet-beta.solana.com\""
            buildConfigField "String", "SOLANA_COMMITMENT", "\"finalized\""
            buildConfigField "String", "OKTA_CLIENT_ID", "\"0oa7ajzug1y8O0CPe357\""
            buildConfigField "String", "OKTA_ISSUER_ID", "\"aus7834x2w3eqeZSr357\""
            buildConfigField "String", "REDIRECT_SUFFIX", "\"\""
            resValue "string", "RAYGUN_APP_ID", "\"vuxX53AURVfZS87D1WPqeg\""
            debuggable false
        }
        demoOne {
            resValue "string", "app_name", "Demo1 Strike"
            applicationIdSuffix ".demo"
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://brooklyn-demo1.strikeprotocols.com/\""
            buildConfigField "String", "ANCHOR_URL", "\"https://demo1.strikeprotocols.com/\""
            buildConfigField "String", "SOLANA_URL", "\"https://api.mainnet-beta.solana.com\""
            buildConfigField "String", "SOLANA_COMMITMENT", "\"finalized\""
            buildConfigField "String", "OKTA_CLIENT_ID", "\"0oa7ajsmclS0GyzMw357\""
            buildConfigField "String", "OKTA_ISSUER_ID", "\"aus7834x2w3eqeZSr357\""
            buildConfigField "String", "REDIRECT_SUFFIX", "\"-demo\""
            resValue "string", "RAYGUN_APP_ID", "\"CtOnGQjIo1U8dELkoUf0iw\""
            debuggable false
        }
        demoTwo {
            initWith demoOne
            resValue "string", "app_name", "Demo2 Strike"
            applicationIdSuffix ".demo2"
            buildConfigField "String", "BASE_URL", "\"https://brooklyn-demo2.strikeprotocols.com/\""
            buildConfigField "String", "ANCHOR_URL", "\"https://demo2.strikeprotocols.com/\""
            buildConfigField "String", "SOLANA_URL", "\"https://solana-validator-demo2.strikeprotocols.com\""
            buildConfigField "String", "SOLANA_COMMITMENT", "\"confirmed\""
            buildConfigField "String", "OKTA_CLIENT_ID", "\"0oa7ajsmclS0GyzMw357\""
            buildConfigField "String", "REDIRECT_SUFFIX", "\"-demo-2\""
        }
        preRelease {
            resValue "string", "app_name", "PreRelease Strike"
            applicationIdSuffix ".prerelease"
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://brooklyn-prerelease.strikeprotocols.com/\""
            buildConfigField "String", "ANCHOR_URL", "\"https://prerelease.strikeprotocols.com/\""
            buildConfigField "String", "SOLANA_URL", "\"https://solana-validator-prerelease.strikeprotocols.com\""
            buildConfigField "String", "SOLANA_COMMITMENT", "\"confirmed\""
            buildConfigField "String", "OKTA_CLIENT_ID", "\"0oa73mddnlx5MMo7j357\""
            buildConfigField "String", "OKTA_ISSUER_ID", "\"aus783fl9oAoVKJv7357\""
            buildConfigField "String", "REDIRECT_SUFFIX", "\"-dev\""
            resValue "string", "RAYGUN_APP_ID", "\"L9T2bPaEjr3Lede3SNpFJw\""
            debuggable false
        }
        auth {
            initWith preRelease
            resValue "string", "app_name", "Auth Strike"
            applicationIdSuffix ".auth"
            minifyEnabled false
            debuggable false
        }
        debug {
            initWith preRelease
            resValue "string", "app_name", "Debug Strike"
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/okta/*'
            excludes += 'META-INF/DEPENDENCIES'
        }
    }
    lint {
        checkAllWarnings true
        lintConfig file('lint.xml')
        warningsAsErrors true
    }
    lintOptions {
        disable 'GradleDependency'
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.activity:activity-compose:$compose_activity"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Compose Specific dependencies
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$compose_lifecycle"
    implementation "androidx.navigation:navigation-compose:2.4.2"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"


    //Compose Accompanist
    implementation "com.google.accompanist:accompanist-flowlayout:0.17.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.23.1"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    //Firebase BOM
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"

    //Push Notifications
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //Okta
    implementation 'com.okta.android:okta-oidc-android:1.2.6'

    //Encrypted Preferences
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha03'

    // Crypto utils
    implementation "org.web3j:core:5.0.0"

    //Okta + Auth utils
    implementation 'com.okta.authn.sdk:okta-authn-sdk-api:2.0.7'
    implementation('com.okta.authn.sdk:okta-authn-sdk-impl:2.0.7') {
        exclude group: 'com.okta.sdk', module: 'okta-sdk-httpclient'
    }

    implementation 'com.okta.sdk:okta-sdk-okhttp:2.0.0'
    implementation 'com.auth0.android:jwtdecode:2.0.1'

    //Biometrics
    implementation("androidx.biometric:biometric:1.1.0")

    // BIP39
    implementation "cash.z.ecc.android:kotlin-bip39:1.0.2"

    //Raygun crash reporting
    implementation 'com.raygun:raygun4android:4.0.1'

    //Unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.mockito:mockito-inline:4.6.1'

    //UI testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}